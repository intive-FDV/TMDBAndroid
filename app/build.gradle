plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id "org.sonarqube" version "3.3"
}

android {
    compileSdk 30
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def _major=0
    def _minor=0
    def _patch=0

    def versionPropsFile = file('version.properties')
    def _versionCodeHash   = 'git rev-parse --verify --short HEAD'.execute().text.trim()
    def _versionCode   = 'git rev-list --all --count'.execute().text.trim().toInteger()
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        _patch = versionProps['PATCH'].toInteger()
        _major = versionProps['MAJOR'].toInteger()
        _minor = versionProps['MINOR'].toInteger()
    }
    else {
        throw new GradleException("Could not read version.properties!")
    }

    def _versionName = "${_major}.${_minor}.${_patch}.${_versionCodeHash}"

    defaultConfig {
        applicationId "com.intive.tmdbandroid"
        minSdk 21
        targetSdk 30
        versionCode _versionCode
        versionName _versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "API_BASE_URL", "\"https://api.themoviedb.org/3/\"")
        buildConfigField("String", "API_KEY", "\"dc41c63c639111ed0845809d99175035\"")
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileNaming = "TMDBAndroid"
            if (variant.buildType.name.equals("release"))
                fileNaming = fileNaming+"-Release"
            else
                fileNaming = fileNaming+"-Debug"

            outputFileName = fileNaming+"-" + _versionName+".apk"

        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileSdkVersion targetSdkVersion
}

dependencies {
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "androidx.core:core-ktx:$coreKTXVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$archLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$archLifecycleVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLoggingVersion"
    implementation "androidx.paging:paging-runtime-ktx:$pagingVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$archLifecycleVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "androidx.palette:palette-ktx:$paletteVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    testImplementation "androidx.arch.core:core-testing:$archTestingVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "androidx.test.ext:truth:$truthVersion"
    testImplementation "app.cash.turbine:turbine:$turbineVersion"
    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}